  BEGIN PGM ONDER_TEST MM
  ;  
  ; WERKSTUK: TOP PLATE 1103_040_002_G2_ONDER
  ;  
  ; BLK FORM 0.1 Z X-29 Y-32.8 Z-8
  ; BLK FORM 0.2 X+29 Y+32.8 Z+2
  ;  
  ; OPSPANNING OP ONDERLEGPLAAT: 
  ; - ONDERLEGPLAAT OPSPANNEN MET 4x SHIM RING 6x16x1 
  ; - BASIC ROTATION OP NUL 
  ; - Z-REF OP BOVENZIJDE ONDERLEGPLAAT (Z=-8) 
  ; - X/Y-REF IN MIDDEN VAN POCKET 
  ;  
  ;  

  
  M36 ; SPAANAFVOER AAN 
  M38 ; AFZUIGING AAN 
  ;  
  ; AANPASSING VOOR FREZEN MET NIEUWE ONDERLEGPLAAT (OK VOOR  ~
    XLS-3-40/60/120): 
  ; - Z-REF RUW MATERIAAL Z-8/+2 -> COORD. OPP. EN DIEPTE AANPASSEN VAN  ~
    EERSTE BEWERKINGEN 
  ; - PLAATSING FIXATIEPUNTEN GEWIJZIGD VOOR XLS-3-80/120 
  ;  
  ; AANPASSINGEN VOOR VERSNELDE PRODUCTIE NOG DOORVOEREN (OK VOOR  ~
    XLS-3-40/60/80) 
  ;  
  ; AANPASSINGEN VOOR OMZETTING NAAR TOOLGEBASEERD PYTHON SCRIPT: 
  ; - RETRACTIE BIJ AFSCHUINEN 
  ;   
  ; ------------------------------- 
  ; #1 - RUWBEWERKING 
  ; ------------------------------- 
  ;  
  ; #1.1 - PILOOTBOREN VOOR BUITENCONTOUR FREZEN 
  ;  
  TOOL CALL 29 Z S6600 ; BOOR D2.9 - ATORN 12280477 
  M3 M8
  ;  
  CYCL DEF 200 BOREN ~
    Q200=1    ;VEILIGHEIDSAFSTAND ~
    Q201=-12  ;DIEPTE ~
    Q206=260  ;AANZET DIEPTEVERPL. ~
    Q202=3    ;DIEPTEVERPLAATSING ~
    Q210=0    ;STILSTANDSTIJD BOVEN ~
    Q203=+2   ;COORD. OPPERVLAK ~
    Q204=25   ;2e VEILIGHEIDSAFST. ~
    Q211=0    ;STILSTANDSTIJD ONDER
  L X+0 Y+25.8 Z+10 F MAX M99
  L X-22 Y+20 Z+10 F MAX M99
  L X+22 Y-20 Z+10 F MAX M99
  L X-22 Y-15 Z+10 F MAX M99
  L X+22 Y+15 Z+10 F MAX M99
  FN 0: Q201 = -5.5 ; DIEPTE 
  L X+0 Y-2.6 Z+4.3 F MAX M99 ; CENTRALE SLEUF Z-3.5 
  FN 0: Q201 = -6 ; DIEPTE 
  L X+0 Y+6.55 Z+2 F MAX M99 ; CENTRALE SLEUF DEEL Z-4 
  ;  
  
  ; 
  ; #1.3 - CENTRALE SLEUF Z-3.5 
  ;  
  TOOL CALL 10 Z S11500 ; FREES D10 - WNT 53 625 10100 
  M3 M8
  CYCL DEF 4.0 KAMERFREZEN
  CYCL DEF 4.1 AFST.  1
  CYCL DEF 4.2 DIEPTE -5.45
  CYCL DEF 4.3 VERPL.  2 F300
  CYCL DEF 4.4 X+52
  CYCL DEF 4.5 Y32.9
  CYCL DEF 4.6 F1000 DR- RADIUS 0
  L X+0 Y-2.6 Z+3 F MAX M99
  ;  
  ; #1.4 - CENTRALE SLEUF Z-4 
  ;  
  TOOL CALL 10 Z S11500 ; FREES D10 - WNT 53 625 10100 
  M3 M8
  CYCL DEF 4.0 KAMERFREZEN
  CYCL DEF 4.1 AFST.  4.5
  CYCL DEF 4.2 DIEPTE -0.5
  CYCL DEF 4.3 VERPL.  0.5 F300
  CYCL DEF 4.4 X+52
  CYCL DEF 4.5 Y14.7
  CYCL DEF 4.6 F1000 DR- RADIUS 0
  L X+0 Y+6.55 Z+1 F MAX M99
  ;  
  ; #1.5 - LAGERZITTING NIET-TRACTIEZIJDE 
  ;  
  TOOL CALL 10 Z S11500 ; FREES D10 - WNT 53 625 10100 
  M3 M8
  CYCL DEF 210 SLEUF PENDELEND ~
    Q200=1    ;VEILIGHEIDSAFSTAND ~
    Q201=-2.85 ;DIEPTE ~
    Q207=1500 ;AANZET FREZEN ~
    Q202=1.6  ;DIEPTEVERPLAATSING ~
    Q215=1    ;BEWERKINGSOMVANG ~
    Q203=+2   ;COORD. OPPERVLAK ~
    Q204=10   ;2e VEILIGHEIDSAFST. ~
    Q216=+0   ;MIDDEN 1e AS ~
    Q217=+20.35 ;MIDDEN 2e AS ~
    Q218=+52  ;LENGTE 1e ZIJKANT ~
    Q219=10.05 ;LENGTE 2e ZIJKANT ~
    Q224=+0   ;ROTATIEPOSITIE ~
    Q338=1.6  ;AANZET NABEWERKING
  L X+Q216 Y+Q217 Z+10 F MAX M99
  ;  
  ; #1.6 - KRAAG TRACTIEZIJDE Z-1.3 
  ;  
  TOOL CALL 10 Z S11500 ; FREES D10 - WNT 53 625 10100 
  M3 M8
  CYCL DEF 210 SLEUF PENDELEND ~
    Q200=1    ;VEILIGHEIDSAFSTAND ~
    Q201=-3.3 ;DIEPTE ~
    Q207=1500 ;AANZET FREZEN ~
    Q202=1.8  ;DIEPTEVERPLAATSING ~
    Q215=1    ;BEWERKINGSOMVANG ~
    Q203=+2   ;COORD. OPPERVLAK ~
    Q204=10   ;2e VEILIGHEIDSAFST. ~
    Q216=+0   ;MIDDEN 1e AS ~
    Q217=-21.45 ;MIDDEN 2e AS ~
    Q218=+52   ;LENGTE 1e ZIJKANT ~
    Q219=10.05 ;LENGTE 2e ZIJKANT ~
    Q224=+0   ;ROTATIEPOSITIE ~
    Q338=1.8  ;AANZET NABEWERKING
  L X+Q216 Y+Q217 Z+10 F MAX M99
  ;  
  ; #1.7 - KRAAG NIET-TRACTIEZIJDE Z0 
  ;  
  TOOL CALL 10 Z S11500 ; FREES D10 - WNT 53 625 10100 
  M3 M8
  CYCL DEF 210 SLEUF PENDELEND ~
    Q200=1    ;VEILIGHEIDSAFSTAND ~
    Q201=-2   ;DIEPTE ~
    Q207=1500 ;AANZET FREZEN ~
    Q202=2.3  ;DIEPTEVERPLAATSING ~
    Q215=1    ;BEWERKINGSOMVANG ~
    Q203=+2   ;COORD. OPPERVLAK ~
    Q204=10   ;2e VEILIGHEIDSAFST. ~
    Q216=+0   ;MIDDEN 1e AS ~
    Q217=+14.6 ;MIDDEN 2e AS ~
    Q218=+52   ;LENGTE 1e ZIJKANT ~
    Q219=10.05 ;LENGTE 2e ZIJKANT ~
    Q224=+0   ;ROTATIEPOSITIE ~
    Q338=0    ;AANZET NABEWERKING
  L X+Q216 Y+Q217 Z+10 F MAX M99
  ;   
  ; #1.8 - BUITENCONTOUR FREZEN 
  ;  
  TOOL CALL 4 Z S28500 ; FREES D4 - WNT 53 635 04300 
  M3 M8
  CYCL DEF 14.0 CONTOUR
  CYCL DEF 14.1 CONTOURLABEL 1
  CYCL DEF 25.0 CONTOURREEKS ~
    Q1=-8     ;FREESDIEPTE ~
    Q3=+0     ;OVERMAAT ZIJKANT ~
    Q5=+2     ;COORD. OPPERVLAK ~
    Q7=+4.3   ;VEILIGE HOOGTE ~
    Q10=+1.5  ;DIEPTEVERPLAATSING ~
    Q11=300   ;AANZET DIEPTEVERPLAATSING ~
    Q12=1000  ;AANZET FREZEN ~
    Q15=+1    ;FREESWIJZE
  L X+0 Y+0 Z+10 F MAX M99
  ;  
  ; #1.9 - TRAPJE VRIJLOOP LAGERZITTING NIET-TRACTIEZIJDE 
  ;  
  TOOL CALL 4 Z S28500 ; FREES D4 - WNT 53 635 04300 
  M3 M8
  CYCL DEF 210 SLEUF PENDELEND ~
    Q200=1    ;VEILIGHEIDSAFSTAND ~
    Q201=-0.4 ;DIEPTE ~
    Q207=1000 ;AANZET FREZEN ~
    Q202=0.4  ;DIEPTEVERPLAATSING ~
    Q215=0    ;BEWERKINGSOMVANG ~
    Q203=-0.9 ;COORD. OPPERVLAK ~
    Q204=10   ;2e VEILIGHEIDSAFST. ~
    Q216=+0   ;MIDDEN 1e AS ~
    Q217=+22.1 ;MIDDEN 2e AS ~
    Q218=+48   ;LENGTE 1e ZIJKANT ~
    Q219=6    ;LENGTE 2e ZIJKANT ~
    Q224=+0   ;ROTATIEPOSITIE ~
    Q338=0    ;AANZET NABEWERKING
  L X+Q216 Y+Q217 Z+10 F MAX M99
  ;  
  ; #1.10 - SLEUF ENCODER Z-4.55 FREZEN 
  ;  
  TOOL CALL 4 Z S28500 ; FREES D4 - WNT 53 635 04300 
  M3 M8
  CYCL DEF 210 SLEUF PENDELEND ~
    Q200=1    ;VEILIGHEIDSAFSTAND ~
    Q201=-0.53 ;DIEPTE ~
    Q207=1000 ;AANZET FREZEN ~
    Q202=0.53 ;DIEPTEVERPLAATSING ~
    Q215=0    ;BEWERKINGSOMVANG ~
    Q203=-4   ;COORD. OPPERVLAK ~
    Q204=10   ;2e VEILIGHEIDSAFST. ~
    Q216=+0   ;MIDDEN 1e AS ~
    Q217=+6.45 ;MIDDEN 2e AS ~
    Q218=+48   ;LENGTE 1e ZIJKANT ~
    Q219=9.5  ;LENGTE 2e ZIJKANT ~
    Q224=+0   ;ROTATIEPOSITIE ~
    Q338=0    ;AANZET NABEWERKING
  L X+Q216 Y+Q217 Z+10 F MAX M99
  ;    
  ; ------------------------------ 
  ; #2 - BOREN 
  ; ------------------------------ 
  ;  
  ; #2.1 - BOREN DIA. 2.4 MM 
  ;  
  TOOL CALL 24 Z S20000 ; BOOR D2,4 - WNT 11 770 02400 
  M3 M8
  ; NIET-TRACTIEZIJDE 
  CYCL DEF 200 BOREN ~
    Q200=1    ;VEILIGHEIDSAFSTAND ~
    Q201=-8   ;DIEPTE ~
    Q206=200  ;AANZET DIEPTEVERPL. ~
    Q202=2    ;DIEPTEVERPLAATSING ~
    Q210=0    ;STILSTANDSTIJD BOVEN ~
    Q203=-0.9 ;COORD. OPPERVLAK ~
    Q204=10   ;2e VEILIGHEIDSAFST. ~
    Q211=0    ;STILSTANDSTIJD ONDER
  CYCL DEF 221 MODEL OP LIJN ~
    Q225=-15  ;STARTPUNT 1e AS ~
    Q226=+17.1 ;STARTPUNT 2e AS ~
    Q237=+10  ;AFSTAND 1e AS ~
    Q238=+0   ;AFSTAND 2e AS ~
    Q242=+4    ;AANTAL KOLOMMEN ~
    Q243=1    ;AANTAL REGELS ~
    Q224=+0   ;ROTATIEPOSITIE ~
    Q200=2    ;VEILIGHEIDSAFSTAND ~
    Q203=-0.9 ;COORD. OPPERVLAK ~
    Q204=10   ;2e VEILIGHEIDSAFST. ~
    Q301=1    ;VERPL.VEILIGH.HOOGTE
  ; TRACTIEZIJDE 
  CYCL DEF 200 BOREN ~
    Q200=1    ;VEILIGHEIDSAFSTAND ~
    Q201=-5   ;DIEPTE ~
    Q206=200  ;AANZET DIEPTEVERPL. ~
    Q202=2    ;DIEPTEVERPLAATSING ~
    Q210=0    ;STILSTANDSTIJD BOVEN ~
    Q203=-3.5 ;COORD. OPPERVLAK ~
    Q204=10   ;2e VEILIGHEIDSAFST. ~
    Q211=0    ;STILSTANDSTIJD ONDER
  CYCL DEF 221 MODEL OP LIJN ~
    Q225=-15  ;STARTPUNT 1e AS ~
    Q226=-17.1 ;STARTPUNT 2e AS ~
    Q237=+10  ;AFSTAND 1e AS ~
    Q238=+0   ;AFSTAND 2e AS ~
    Q242=+4   ;AANTAL KOLOMMEN ~
    Q243=1    ;AANTAL REGELS ~
    Q224=+0   ;ROTATIEPOSITIE ~
    Q200=1    ;VEILIGHEIDSAFSTAND ~
    Q203=-3.5 ;COORD. OPPERVLAK ~
    Q204=10   ;2e VEILIGHEIDSAFST. ~
    Q301=1    ;VERPL.VEILIGH.HOOGTE
  ;  
  ; #2.2 - BOREN M2.5 
  ;  
  TOOL CALL 21 Z S25000 ; BOOR D2,1 - WNT 11 770 02100 
  M3 M8
  CYCL DEF 200 BOREN ~
    Q200=1    ;VEILIGHEIDSAFSTAND ~
    Q201=-10  ;DIEPTE ~
    Q206=250  ;AANZET DIEPTEVERPL. ~
    Q202=1.5  ;DIEPTEVERPLAATSING ~
    Q210=0    ;STILSTANDSTIJD BOVEN ~
    Q203=-1.3 ;COORD. OPPERVLAK ~
    Q204=10   ;2e VEILIGHEIDSAFST. ~
    Q211=0    ;STILSTANDSTIJD ONDER
  CYCL DEF 221 MODEL OP LIJN ~
    Q225=-15  ;STARTPUNT 1e AS ~
    Q226=-22  ;STARTPUNT 2e AS ~
    Q237=+10  ;AFSTAND 1e AS ~
    Q238=+44  ;AFSTAND 2e AS ~
    Q242=+4    ;AANTAL KOLOMMEN ~
    Q243=2    ;AANTAL REGELS ~
    Q224=+0   ;ROTATIEPOSITIE ~
    Q200=1    ;VEILIGHEIDSAFSTAND ~
    Q203=-1.3 ;COORD. OPPERVLAK ~
    Q204=10   ;2e VEILIGHEIDSAFST. ~
    Q301=1    ;VERPL.VEILIGH.HOOGTE
  ;  
  ; #2.3 - BOREN M2 
  ;  
  TOOL CALL 16 Z S30000 ; BOOR D1,6 - WNT 11 770 01600 
  M3 M8
  ;  
  
  ;  
  ; GATENPATROON BEVESTING T-LAT 
  CYCL DEF 200 BOREN ~
    Q200=1    ;VEILIGHEIDSAFSTAND ~
    Q201=-7.5 ;DIEPTE ~
    Q206=300  ;AANZET DIEPTEVERPL. ~
    Q202=1    ;DIEPTEVERPLAATSING ~
    Q210=0    ;STILSTANDSTIJD BOVEN ~
    Q203=-3.5 ;COORD. OPPERVLAK ~
    Q204=10   ;2e VEILIGHEIDSAFST. ~
    Q211=0    ;STILSTANDSTIJD ONDER
  CYCL DEF 221 MODEL OP LIJN ~
    Q225=-15  ;STARTPUNT 1e AS ~
    Q226=-7.8 ;STARTPUNT 2e AS ~
    Q237=+10  ;AFSTAND 1e AS ~
    Q238=+0   ;AFSTAND 2e AS ~
    Q242=+4    ;AANTAL KOLOMMEN ~
    Q243=1    ;AANTAL REGELS ~
    Q224=+0   ;ROTATIEPOSITIE ~
    Q200=1    ;VEILIGHEIDSAFSTAND ~
    Q203=-3.5 ;COORD. OPPERVLAK ~
    Q204=10   ;2e VEILIGHEIDSAFST. ~
    Q301=1    ;VERPL.VEILIGH.HOOGTE
  ;  
  ; GATENPATROON EINDESLAG 
  CYCL DEF 200 BOREN ~
    Q200=1    ;VEILIGHEIDSAFSTAND ~
    Q201=-7.5 ;DIEPTE ~
    Q206=300  ;AANZET DIEPTEVERPL. ~
    Q202=1    ;DIEPTEVERPLAATSING ~
    Q210=0    ;STILSTANDSTIJD BOVEN ~
    Q203=-3.5 ;COORD. OPPERVLAK ~
    Q204=10   ;2e VEILIGHEIDSAFST. ~
    Q211=0    ;STILSTANDSTIJD ONDER
  L X-7 Y-3.5 Z+10 F MAX M99
L X-4.5 Y-3.5 Z+10 F MAX M99
L X-2 Y-3.5 Z+10 F MAX M99
L X+2 Y-3.5 Z+10 F MAX M99
L X+4.5 Y-3.5 Z+10 F MAX M99
L X+7 Y-3.5 Z+10 F MAX M99

  ;  
  ; #2.4 - BOREN M1.6 
  ;  
  TOOL CALL 12 Z S35000 ; BOOR D1,3 - WNT 11 770 01300 
  M3 M8
  ;  
  ; PATROON 20 X 30.4 MM (ENKEL TRACTIEZIJDE) 
  CYCL DEF 200 BOREN ~
    Q200=1    ;VEILIGHEIDSAFSTAND ~
    Q201=-7   ;DIEPTE ~
    Q206=260  ;AANZET DIEPTEVERPL. ~
    Q202=1    ;DIEPTEVERPLAATSING ~
    Q210=0    ;STILSTANDSTIJD BOVEN ~
    Q203=-3.5 ;COORD. OPPERVLAK ~
    Q204=10   ;2e VEILIGHEIDSAFST. ~
    Q211=0    ;STILSTANDSTIJD ONDER
  CYCL DEF 221 MODEL OP LIJN ~
    Q225=-10  ;STARTPUNT 1e AS ~
    Q226=-15.2 ;STARTPUNT 2e AS ~
    Q237=+20  ;AFSTAND 1e AS ~
    Q238=+0   ;AFSTAND 2e AS ~
    Q242=2    ;AANTAL KOLOMMEN ~
    Q243=1    ;AANTAL REGELS ~
    Q224=+0   ;ROTATIEPOSITIE ~
    Q200=1    ;VEILIGHEIDSAFSTAND ~
    Q203=-3.5 ;COORD. OPPERVLAK ~
    Q204=10   ;2e VEILIGHEIDSAFST. ~
    Q301=1    ;VERPL.VEILIGH.HOOGTE
  ;  
  ; PATROON 20 X 13 MM 
  CYCL DEF 200 BOREN ~
    Q200=1    ;VEILIGHEIDSAFSTAND ~
    Q201=-7   ;DIEPTE ~
    Q206=260  ;AANZET DIEPTEVERPL. ~
    Q202=1    ;DIEPTEVERPLAATSING ~
    Q210=0    ;STILSTANDSTIJD BOVEN ~
    Q203=-3.5 ;COORD. OPPERVLAK ~
    Q204=10   ;2e VEILIGHEIDSAFST. ~
    Q211=0    ;STILSTANDSTIJD ONDER
  CYCL DEF 221 MODEL OP LIJN ~
    Q225=-10  ;STARTPUNT 1e AS ~
    Q226=-6.5 ;STARTPUNT 2e AS ~
    Q237=+20  ;AFSTAND 1e AS ~
    Q238=+0   ;AFSTAND 2e AS ~
    Q242=2    ;AANTAL KOLOMMEN ~
    Q243=1    ;AANTAL REGELS ~
    Q224=+0   ;ROTATIEPOSITIE ~
    Q200=1    ;VEILIGHEIDSAFSTAND ~
    Q203=-3.5 ;COORD. OPPERVLAK ~
    Q204=10   ;2e VEILIGHEIDSAFST. ~
    Q301=1    ;VERPL.VEILIGH.HOOGTE
  CYCL DEF 200 BOREN ~
    Q200=1    ;VEILIGHEIDSAFSTAND ~
    Q201=-6   ;DIEPTE ~
    Q206=260  ;AANZET DIEPTEVERPL. ~
    Q202=1    ;DIEPTEVERPLAATSING ~
    Q210=0    ;STILSTANDSTIJD BOVEN ~
    Q203=-4.55 ;COORD. OPPERVLAK ~
    Q204=10   ;2e VEILIGHEIDSAFST. ~
    Q211=0    ;STILSTANDSTIJD ONDER
  CYCL DEF 221 MODEL OP LIJN ~
    Q225=-10  ;STARTPUNT 1e AS ~
    Q226=+6.5 ;STARTPUNT 2e AS ~
    Q237=+20  ;AFSTAND 1e AS ~
    Q238=+0   ;AFSTAND 2e AS ~
    Q242=2    ;AANTAL KOLOMMEN ~
    Q243=1    ;AANTAL REGELS ~
    Q224=+0   ;ROTATIEPOSITIE ~
    Q200=1    ;VEILIGHEIDSAFSTAND ~
    Q203=-4.55 ;COORD. OPPERVLAK ~
    Q204=10   ;2e VEILIGHEIDSAFST. ~
    Q301=1    ;VERPL.VEILIGH.HOOGTE
  ;  
  ; PATROON 30.4 X 20 MM 
  CYCL DEF 200 BOREN ~
    Q200=1    ;VEILIGHEIDSAFSTAND ~
    Q201=-7   ;DIEPTE ~
    Q206=260  ;AANZET DIEPTEVERPL. ~
    Q202=1    ;DIEPTEVERPLAATSING ~
    Q210=0    ;STILSTANDSTIJD BOVEN ~
    Q203=-3.5 ;COORD. OPPERVLAK ~
    Q204=10   ;2e VEILIGHEIDSAFST. ~
    Q211=0    ;STILSTANDSTIJD ONDER
  CYCL DEF 221 MODEL OP LIJN ~
    Q225=-15.2 ;STARTPUNT 1e AS ~
    Q226=-10  ;STARTPUNT 2e AS ~
    Q237=+30.4 ;AFSTAND 1e AS ~
    Q238=+0   ;AFSTAND 2e AS ~
    Q242=2    ;AANTAL KOLOMMEN ~
    Q243=1    ;AANTAL REGELS ~
    Q224=+0   ;ROTATIEPOSITIE ~
    Q200=1    ;VEILIGHEIDSAFSTAND ~
    Q203=-3.5 ;COORD. OPPERVLAK ~
    Q204=10   ;2e VEILIGHEIDSAFST. ~
    Q301=1    ;VERPL.VEILIGH.HOOGTE
  CYCL DEF 200 BOREN ~
    Q200=1    ;VEILIGHEIDSAFSTAND ~
    Q201=-6   ;DIEPTE ~
    Q206=260  ;AANZET DIEPTEVERPL. ~
    Q202=1    ;DIEPTEVERPLAATSING ~
    Q210=0    ;STILSTANDSTIJD BOVEN ~
    Q203=-4.55 ;COORD. OPPERVLAK ~
    Q204=10   ;2e VEILIGHEIDSAFST. ~
    Q211=0    ;STILSTANDSTIJD ONDER
  CYCL DEF 221 MODEL OP LIJN ~
    Q225=-15.2 ;STARTPUNT 1e AS ~
    Q226=+10  ;STARTPUNT 2e AS ~
    Q237=+30.4 ;AFSTAND 1e AS ~
    Q238=+0   ;AFSTAND 2e AS ~
    Q242=2    ;AANTAL KOLOMMEN ~
    Q243=1    ;AANTAL REGELS ~
    Q224=+0   ;ROTATIEPOSITIE ~
    Q200=1    ;VEILIGHEIDSAFSTAND ~
    Q203=-4.55 ;COORD. OPPERVLAK ~
    Q204=10   ;2e VEILIGHEIDSAFST. ~
    Q301=1    ;VERPL.VEILIGH.HOOGTE
  ;  
  ; ------------------------------ 
  ; #3 - AFSCHUINEN GATEN 
  ; ------------------------------ 
  ;  
  ; #3.1 - AFSCHUINEN DIA. 2.4 MM 
  ;  
  TOOL CALL 43 Z S5000 ; CENTERBOOR D3 - WNT 10 702 003  
  M3
  ; NIET-TRACTIEZIJDE 
  CYCL DEF 200 BOREN ~
    Q200=1    ;VEILIGHEIDSAFSTAND ~
    Q201=-0.3 ;DIEPTE ~
    Q206=300  ;AANZET DIEPTEVERPL. ~
    Q202=0.3  ;DIEPTEVERPLAATSING ~
    Q210=0    ;STILSTANDSTIJD BOVEN ~
    Q203=-1.9 ;COORD. OPPERVLAK ~
    Q204=10   ;2e VEILIGHEIDSAFST. ~
    Q211=0    ;STILSTANDSTIJD ONDER
  CYCL DEF 221 MODEL OP LIJN ~
    Q225=-15  ;STARTPUNT 1e AS ~
    Q226=+17.1 ;STARTPUNT 2e AS ~
    Q237=+10  ;AFSTAND 1e AS ~
    Q238=+0   ;AFSTAND 2e AS ~
    Q242=+4    ;AANTAL KOLOMMEN ~
    Q243=1    ;AANTAL REGELS ~
    Q224=+0   ;ROTATIEPOSITIE ~
    Q200=1    ;VEILIGHEIDSAFSTAND ~
    Q203=-1.9 ;COORD. OPPERVLAK ~
    Q204=10   ;2e VEILIGHEIDSAFST. ~
    Q301=1    ;VERPL.VEILIGH.HOOGTE
  ; TRACTIEZIJDE 
  CYCL DEF 200 BOREN ~
    Q200=1    ;VEILIGHEIDSAFSTAND ~
    Q201=-0.3 ;DIEPTE ~
    Q206=300  ;AANZET DIEPTEVERPL. ~
    Q202=0.3  ;DIEPTEVERPLAATSING ~
    Q210=0    ;STILSTANDSTIJD BOVEN ~
    Q203=-4.5 ;COORD. OPPERVLAK ~
    Q204=10   ;2e VEILIGHEIDSAFST. ~
    Q211=0    ;STILSTANDSTIJD ONDER
  CYCL DEF 221 MODEL OP LIJN ~
    Q225=-15  ;STARTPUNT 1e AS ~
    Q226=-17.1 ;STARTPUNT 2e AS ~
    Q237=+10  ;AFSTAND 1e AS ~
    Q238=+0   ;AFSTAND 2e AS ~
    Q242=+4   ;AANTAL KOLOMMEN ~
    Q243=1    ;AANTAL REGELS ~
    Q224=+0   ;ROTATIEPOSITIE ~
    Q200=1    ;VEILIGHEIDSAFSTAND ~
    Q203=-4.5 ;COORD. OPPERVLAK ~
    Q204=10   ;2e VEILIGHEIDSAFST. ~
    Q301=1    ;VERPL.VEILIGH.HOOGTE
  ;  
  ; #3.2 - AFSCHUINEN GATEN M2.5 
  ;  
  TOOL CALL 43 Z S5000 ; CENTERBOOR D3 - WNT 10 702 003 
  M3
  CYCL DEF 200 BOREN ~
    Q200=1    ;VEILIGHEIDSAFSTAND ~
    Q201=-0.35 ;DIEPTE ~
    Q206=300  ;AANZET DIEPTEVERPL. ~
    Q202=0.35 ;DIEPTEVERPLAATSING ~
    Q210=0    ;STILSTANDSTIJD BOVEN ~
    Q203=-2.3 ;COORD. OPPERVLAK ~
    Q204=10   ;2e VEILIGHEIDSAFST. ~
    Q211=0    ;STILSTANDSTIJD ONDER
  CYCL DEF 221 MODEL OP LIJN ~
    Q225=-15  ;STARTPUNT 1e AS ~
    Q226=-22  ;STARTPUNT 2e AS ~
    Q237=+10  ;AFSTAND 1e AS ~
    Q238=+44  ;AFSTAND 2e AS ~
    Q242=+4    ;AANTAL KOLOMMEN ~
    Q243=2    ;AANTAL REGELS ~
    Q224=+0   ;ROTATIEPOSITIE ~
    Q200=1    ;VEILIGHEIDSAFSTAND ~
    Q203=-2.3 ;COORD. OPPERVLAK ~
    Q204=10   ;2e VEILIGHEIDSAFST. ~
    Q301=1    ;VERPL.VEILIGH.HOOGTE
  ;  
  ; #3.3 - AFSCHUINEN GATEN M2 
  ;  
  TOOL CALL 43 Z S5000 ; CENTERBOOR D3 - WNT 10 702 003 
  M3
  ;  
  ; GATENPATROON BEVESTING T-LAT 
  CYCL DEF 200 BOREN ~
    Q200=1    ;VEILIGHEIDSAFSTAND ~
    Q201=-1   ;DIEPTE ~
    Q206=300  ;AANZET DIEPTEVERPL. ~
    Q202=1    ;DIEPTEVERPLAATSING ~
    Q210=0    ;STILSTANDSTIJD BOVEN ~
    Q203=-3.5 ;COORD. OPPERVLAK ~
    Q204=10   ;2e VEILIGHEIDSAFST. ~
    Q211=0    ;STILSTANDSTIJD ONDER
  CYCL DEF 221 MODEL OP LIJN ~
    Q225=-15  ;STARTPUNT 1e AS ~
    Q226=-7.8 ;STARTPUNT 2e AS ~
    Q237=+10  ;AFSTAND 1e AS ~
    Q238=+0   ;AFSTAND 2e AS ~
    Q242=+4    ;AANTAL KOLOMMEN ~
    Q243=1    ;AANTAL REGELS ~
    Q224=+0   ;ROTATIEPOSITIE ~
    Q200=1    ;VEILIGHEIDSAFSTAND ~
    Q203=-3.5 ;COORD. OPPERVLAK ~
    Q204=10   ;2e VEILIGHEIDSAFST. ~
    Q301=1    ;VERPL.VEILIGH.HOOGTE
  ;  
  ; GATENPATROON EINDESLAG 
  CYCL DEF 200 BOREN ~
    Q200=1    ;VEILIGHEIDSAFSTAND ~
    Q201=-1   ;DIEPTE ~
    Q206=200  ;AANZET DIEPTEVERPL. ~
    Q202=1    ;DIEPTEVERPLAATSING ~
    Q210=0    ;STILSTANDSTIJD BOVEN ~
    Q203=-3.5 ;COORD. OPPERVLAK ~
    Q204=10   ;2e VEILIGHEIDSAFST. ~
    Q211=0    ;STILSTANDSTIJD ONDER
  L X-7 Y-3.5 Z+10 F MAX M99
  
L X-4.5 Y-3.5 Z+10 F MAX M99
  
L X-2 Y-3.5 Z+10 F MAX M99
  
L X+2 Y-3.5 Z+10 F MAX M99
  
L X+4.5 Y-3.5 Z+10 F MAX M99
  
L X+7 Y-3.5 Z+10 F MAX M99
  

  ;  
  ; #3.4 - AFSCHUINEN GATEN M1.6 
  ;  
  TOOL CALL 43 Z S5000 ; CENTERBOOR D3 - WNT 10 702 003 
  M3
  ;  
  ; PATROON 20 X 30.4 MM (ENKEL TRACTIEZIJDE) 
  CYCL DEF 200 BOREN ~
    Q200=1    ;VEILIGHEIDSAFSTAND ~
    Q201=-0.8 ;DIEPTE ~
    Q206=300  ;AANZET DIEPTEVERPL. ~
    Q202=0.8  ;DIEPTEVERPLAATSING ~
    Q210=0    ;STILSTANDSTIJD BOVEN ~
    Q203=-3.5 ;COORD. OPPERVLAK ~
    Q204=10   ;2e VEILIGHEIDSAFST. ~
    Q211=0    ;STILSTANDSTIJD ONDER
  CYCL DEF 221 MODEL OP LIJN ~
    Q225=-10  ;STARTPUNT 1e AS ~
    Q226=-15.2 ;STARTPUNT 2e AS ~
    Q237=+20  ;AFSTAND 1e AS ~
    Q238=+0   ;AFSTAND 2e AS ~
    Q242=2    ;AANTAL KOLOMMEN ~
    Q243=1    ;AANTAL REGELS ~
    Q224=+0   ;ROTATIEPOSITIE ~
    Q200=1    ;VEILIGHEIDSAFSTAND ~
    Q203=-3.5 ;COORD. OPPERVLAK ~
    Q204=10   ;2e VEILIGHEIDSAFST. ~
    Q301=1    ;VERPL.VEILIGH.HOOGTE
  ;  
  ; PATROON 20 X 13 MM 
  CYCL DEF 200 BOREN ~
    Q200=1    ;VEILIGHEIDSAFSTAND ~
    Q201=-0.8 ;DIEPTE ~
    Q206=300  ;AANZET DIEPTEVERPL. ~
    Q202=0.8  ;DIEPTEVERPLAATSING ~
    Q210=0    ;STILSTANDSTIJD BOVEN ~
    Q203=-3.5 ;COORD. OPPERVLAK ~
    Q204=10   ;2e VEILIGHEIDSAFST. ~
    Q211=0    ;STILSTANDSTIJD ONDER
  CYCL DEF 221 MODEL OP LIJN ~
    Q225=-10  ;STARTPUNT 1e AS ~
    Q226=-6.5 ;STARTPUNT 2e AS ~
    Q237=+20  ;AFSTAND 1e AS ~
    Q238=+0   ;AFSTAND 2e AS ~
    Q242=2    ;AANTAL KOLOMMEN ~
    Q243=1    ;AANTAL REGELS ~
    Q224=+0   ;ROTATIEPOSITIE ~
    Q200=1    ;VEILIGHEIDSAFSTAND ~
    Q203=-3.5 ;COORD. OPPERVLAK ~
    Q204=10   ;2e VEILIGHEIDSAFST. ~
    Q301=1    ;VERPL.VEILIGH.HOOGTE
  CYCL DEF 200 BOREN ~
    Q200=1    ;VEILIGHEIDSAFSTAND ~
    Q201=-0.8 ;DIEPTE ~
    Q206=300  ;AANZET DIEPTEVERPL. ~
    Q202=0.8  ;DIEPTEVERPLAATSING ~
    Q210=0    ;STILSTANDSTIJD BOVEN ~
    Q203=-4.55 ;COORD. OPPERVLAK ~
    Q204=10   ;2e VEILIGHEIDSAFST. ~
    Q211=0    ;STILSTANDSTIJD ONDER
  CYCL DEF 221 MODEL OP LIJN ~
    Q225=-10  ;STARTPUNT 1e AS ~
    Q226=+6.5 ;STARTPUNT 2e AS ~
    Q237=+20  ;AFSTAND 1e AS ~
    Q238=+0   ;AFSTAND 2e AS ~
    Q242=2    ;AANTAL KOLOMMEN ~
    Q243=1    ;AANTAL REGELS ~
    Q224=+0   ;ROTATIEPOSITIE ~
    Q200=1    ;VEILIGHEIDSAFSTAND ~
    Q203=-4.55 ;COORD. OPPERVLAK ~
    Q204=10   ;2e VEILIGHEIDSAFST. ~
    Q301=1    ;VERPL.VEILIGH.HOOGTE
  ;  
  ; PATROON 30.4 X 20 MM 
  CYCL DEF 200 BOREN ~
    Q200=1    ;VEILIGHEIDSAFSTAND ~
    Q201=-0.8 ;DIEPTE ~
    Q206=300  ;AANZET DIEPTEVERPL. ~
    Q202=0.8  ;DIEPTEVERPLAATSING ~
    Q210=0    ;STILSTANDSTIJD BOVEN ~
    Q203=-3.5 ;COORD. OPPERVLAK ~
    Q204=10   ;2e VEILIGHEIDSAFST. ~
    Q211=0    ;STILSTANDSTIJD ONDER
  CYCL DEF 221 MODEL OP LIJN ~
    Q225=-15.2 ;STARTPUNT 1e AS ~
    Q226=-10  ;STARTPUNT 2e AS ~
    Q237=+30.4 ;AFSTAND 1e AS ~
    Q238=+0   ;AFSTAND 2e AS ~
    Q242=2    ;AANTAL KOLOMMEN ~
    Q243=1    ;AANTAL REGELS ~
    Q224=+0   ;ROTATIEPOSITIE ~
    Q200=1    ;VEILIGHEIDSAFSTAND ~
    Q203=-3.5 ;COORD. OPPERVLAK ~
    Q204=10   ;2e VEILIGHEIDSAFST. ~
    Q301=1    ;VERPL.VEILIGH.HOOGTE
  CYCL DEF 200 BOREN ~
    Q200=1    ;VEILIGHEIDSAFSTAND ~
    Q201=-0.8 ;DIEPTE ~
    Q206=300  ;AANZET DIEPTEVERPL. ~
    Q202=0.8  ;DIEPTEVERPLAATSING ~
    Q210=0    ;STILSTANDSTIJD BOVEN ~
    Q203=-4.55 ;COORD. OPPERVLAK ~
    Q204=10   ;2e VEILIGHEIDSAFST. ~
    Q211=0    ;STILSTANDSTIJD ONDER
  CYCL DEF 221 MODEL OP LIJN ~
    Q225=-15.2 ;STARTPUNT 1e AS ~
    Q226=+10  ;STARTPUNT 2e AS ~
    Q237=+30.4 ;AFSTAND 1e AS ~
    Q238=+0   ;AFSTAND 2e AS ~
    Q242=2    ;AANTAL KOLOMMEN ~
    Q243=1    ;AANTAL REGELS ~
    Q224=+0   ;ROTATIEPOSITIE ~
    Q200=1    ;VEILIGHEIDSAFSTAND ~
    Q203=-4.55 ;COORD. OPPERVLAK ~
    Q204=10   ;2e VEILIGHEIDSAFST. ~
    Q301=1    ;VERPL.VEILIGH.HOOGTE
  ;  
  ; ------------------------------ 
  ; #4 - VACUUMGROEVEN FREZEN 
  ; ------------------------------ 
  ;  
  TOOL CALL 5 Z S35000 ; FREES D0,6 - ATORN 11860006 
  M3 M8
  ; BOVENSTE RIJ 
  L X-15 Y+20 Z+10 F MAX
  L Z+1 F MAX
  L Z-0.95 F1000
  L Y+17.1 F200
  L Z-1.05 F50
  L Y+20 F200
  
  L X-5 F500
  L Z-0.95 F1000
  L Y+17.1 F200
  L Z-1.05 F50
  L Y+20 F200
  

  L X+5 F500
  L Z-0.95 F1000
  L Y+17.1 F200
  L Z-1.05 F50
  L Y+20 F200
  

  L X+15 F500
  L Z-0.95 F1000
  L Y+17.1 F200
  L Z-1.05 F50
  L Y+20 F200
  

  L Z+1 F1000
  L Z+10 F MAX
  ;  
  ; ------------------------------ 
  ; #5 - STUK LOSFREZEN OP AANTAL FIXATIEPUNTEN NA 
  ; ------------------------------ 
  ;  
  TOOL CALL 4 Z S28500 ; FREES D4 - WNT 53 635 04300 
  M3 M8
  CYCL DEF 14.0 CONTOUR
  CYCL DEF 14.1 CONTOURLABEL 2
  CYCL DEF 25.0 CONTOURREEKS ~
    Q1=-2.5   ;FREESDIEPTE ~
    Q3=+0     ;OVERMAAT ZIJKANT ~
    Q5=-6     ;COORD. OPPERVLAK ~
    Q7=+1     ;VEILIGE HOOGTE ~
    Q10=+1    ;DIEPTEVERPLAATSING ~
    Q11=300   ;AANZET DIEPTEVERPLAATSING ~
    Q12=1000  ;AANZET FREZEN ~
    Q15=+1    ;FREESWIJZE
  L X+0 Y+0 Z+10 F MAX M99
  CYCL DEF 14.0 CONTOUR
  CYCL DEF 14.1 CONTOURLABEL 3
  L X+0 Y+0 Z+10 F MAX M99
  CYCL DEF 14.0 CONTOUR
  CYCL DEF 14.1 CONTOURLABEL 4
  L X+0 Y+0 Z+10 F MAX M99
  CYCL DEF 14.0 CONTOUR
  CYCL DEF 14.1 CONTOURLABEL 5
  L X+0 Y+0 Z+10 F MAX M99
  ;  
  ; ------------------------------ 
  ; #6 - FIJNBEWERKING/FINISSEREN 
  ; ------------------------------ 
  ;  
  ; #6.1 - LIJMGROEVEN FREZEN 
  ;  
  TOOL CALL 2 Z S12000 DL-0.02 ; FREES D2 -  WNT 53 615 02100 
  ; LAAG TOERENTAL EN HOGE FEED -> BETERE HECHTING 
  M3 M8
  ; LIJMGROEF 2 MM 
  L X-22 Y+9 Z+10 F MAX
  L Z+1 F MAX
  L Z-4.6 F1000
  L X+22 F2000
  L Z+2 F1000
  ; LIJMGROEF 4 MM 
  L X-22 Y+3.7 F MAX
  L Z+1 F MAX
  L Z-4.6 F1000
  L X+22 F2000
  L Y+4.7 F300
  L X-22 F2000
  L Y+5.7 F300
  L X+22 F2000
  L Z+1 F1000
  L Z+10 F MAX
  ;  
  ; #6.2 - AFSCHUINEN RANDEN 
  ;  
  TOOL CALL 40 Z S15000 DL-0.8 DR-1.2 ; ONTBRAAM_D4 - WNT 53 660 04000 
  M3
  ; UITLIJNKRAAG NIET-TRACTIEZIJDE 
  L X+0 Y+10 Z+10 R0 F MAX
  L Z+1 F MAX
  G42 D40 ; redo!
  APPR LN X+0 Y+13.9 Z-0.05 LEN+1 RR F1000
  L X+20 F1000
  L Y+15.3 F1000
  L X-20 F1000
  L Y+13.9 F1000
  L X+0 F1000
  DEP LN LEN+1 F1000
  L Z+1 R0 F1000
  ; LAGERZITTING/BUITENRAND NIET-TRACTIEZIJDE 
  L X-22 Y+16.8 Z+1 R0 F MAX
  APPR LN X-20 Y+16.8 Z-1.05 LEN+1 RL F1000
  L Y+19.6 F1000
  L Z-1.45 F1000
  L Y+23.8 F1000
  CHF 0.3 F100
  L X+20 F1000
  CHF 0.3 F100
  L Y+19.6 F1000
  L Z-1.05 F1000
  L Y+16.8 F1000
  DEP LN LEN+1 F1000
  L Z+1 R0 F1000
  ; UITLIJNKRAAG/BUITENRAND TRACTIEZIJDE 
  L X+0 Y-25 Z+1 R0 F MAX
  APPR LN X+0 Y-23.8 Z-1.45 LEN+1 RR F1000
  L X+20 F1000
  CHF 0.3 F100
  L Y-19.1 F1000
  L X-20 F1000
  L Y-23.8 F1000
  CHF 0.3 F100
  L X+0 F1000
  DEP LN LEN+1 F1000
  L Z+1 R0 F1000
  ; RAND CENTRALE SLEUF LINKS 
  L X-22 Y-17.1 Z+1 R0 F MAX
  APPR LN X-20 Y-17.1 Z-3.55 LEN+1 RL F1000
  L Y+2.7 F1000
  L Z-4.7 F1000
  L Y+10.2 F1000
  DEP LN LEN+1 F1000
  L Z+1 R0 F1000
  ; RAND CENTRALE SLEUF RECHTS 
  L X+22 Y-17.1 Z+1 R0 F MAX
  APPR LN X+20 Y-17.1 Z-3.55 LEN+1 RR F1000
  L Y+2.7 F1000
  L Z-4.7 F1000
  L Y+10.2 F1000
  DEP LN LEN+1 F1000
  L Z+1 R0 F1000
  L Z+10 F MAX
  ;  
  ; #6.3 - SLEUF ENCODER Z-4.55 FINISSEREN 
  ;  
  TOOL CALL 14 Z S28500 ; FREES D4 FINISSEER - WNT 53 635 04300 
  M3 M8
  CYCL DEF 4.0 KAMERFREZEN
  CYCL DEF 4.1 AFST.  5
  CYCL DEF 4.2 DIEPTE -0.55
  CYCL DEF 4.3 VERPL.  0.55 F300
  CYCL DEF 4.4 X+46
  CYCL DEF 4.5 Y9.5
  CYCL DEF 4.6 F1000 DR- RADIUS 0
  L X+0 Y+6.45 Z+1 F MAX M99
  ;  
  ; #6.4 - CENTRALE SLEUF Z-4 FINISSEREN 
  ;  
  TOOL CALL 14 Z S28500 ; FREES D4 FINISSEER - WNT 53 635 04300 
  M3 M8
  CYCL DEF 4.0 KAMERFREZEN
  CYCL DEF 4.1 AFST.  4.5
  CYCL DEF 4.2 DIEPTE -0.5
  CYCL DEF 4.3 VERPL.  0.5 F300
  CYCL DEF 4.4 X+46
  CYCL DEF 4.5 Y14.7
  CYCL DEF 4.6 F1000 DR- RADIUS 0
  L X+0 Y+6.55 Z+1 F MAX M99
  ; 
  
  ;
  ; #6.6 - CENTRALE SLEUF Z-3.5 FINISSEREN
  ;
  TOOL CALL 14 Z S28500 ; FREES D4 FINISSEER - WNT 53 635 04300 
  M3 M8
  CYCL DEF 4.0 KAMERFREZEN
  CYCL DEF 4.1 AFST.  1
  CYCL DEF 4.2 DIEPTE -3.5
  CYCL DEF 4.3 VERPL.  3.5 F300
  CYCL DEF 4.4 X+46
  CYCL DEF 4.5 Y19.9
  CYCL DEF 4.6 F1000 DR- RADIUS 0
  L X+0 Y-9.1 Z+1 F MAX M99
  ;  
  ; #6.7 - FINISSEREN LAGERZITTINGEN 
  ;  
  TOOL CALL 14 Z S28500 ; FREES D4 FINISSEER - WNT 53 635 04300 
  M3 M8
  ; LAGERZITTING/UITLIJNKRAAG NIET-TRACTIEZIJDE 
  L X-22 Y+11 Z+10 R0 F MAX
  L Z+1 F MAX
  APPR LN X-19.98 Y+11 Z-0.9 LEN+1 RL F1000
  L Y+15.3 F1000
  CHF 0.1 F100
  L X+19.98 F1000
  CHF 0.1 F100
  L Y+11 F1000
  DEP LN LEN+1 F1000
  L Z+2 F1000
  L Z+10 R0 F MAX
  ; UITLIJNKRAAG TRACTIEZIJDE 
  L X-22.1 Y-25.8 Z+10 R0 F MAX
  L Z+1 F MAX
  APPR LN X-19.98 Y-25.8 Z-3.48 LEN+1 RL F1000
  L Y-19.1 F1000
  CHF 0.1 F100
  L X+19.98 F1000
  CHF 0.1 F100
  L Y-25.8 F1000
  DEP LN LEN+1 F1000
  L Z+1 R0 F1000
  L Z+10 F MAX
  ;  
  ; ------------------------------ 
  ; #7 - TAPPEN 
  ; ------------------------------ 
  ;  
  ; #7.1 - TAPPEN M2.5 
  ;  
  TOOL CALL 55 Z S1200 ; TAP M2.5 - WNT 23 122 025 
  M3 M8
  CYCL DEF 209 SCHRDR.BOR. SPAANBR. ~
    Q200=1    ;VEILIGHEIDSAFSTAND ~
    Q201=-8.5 ;DRAADDIEPTE ~
    Q239=+0.45 ;SPOED ~
    Q203=-1.3 ;COORD. OPPERVLAK ~
    Q204=10   ;2e VEILIGHEIDSAFST. ~
    Q257=8.5  ;BOORDPTE SPAANBREUK ~
    Q256=2    ;TERUGTR.HGT SPAANBR. ~
    Q336=+0   ;HOEK SPIL
  CYCL DEF 221 MODEL OP LIJN ~
    Q225=-15  ;STARTPUNT 1e AS ~
    Q226=-22  ;STARTPUNT 2e AS ~
    Q237=+10  ;AFSTAND 1e AS ~
    Q238=+44  ;AFSTAND 2e AS ~
    Q242=+4    ;AANTAL KOLOMMEN ~
    Q243=2    ;AANTAL REGELS ~
    Q224=+0   ;ROTATIEPOSITIE ~
    Q200=1    ;VEILIGHEIDSAFSTAND ~
    Q203=-1.3 ;COORD. OPPERVLAK ~
    Q204=10   ;2e VEILIGHEIDSAFST. ~
    Q301=1    ;VERPL.VEILIGH.HOOGTE
  ;  
  TCH PROBE 586 WT-BREUKCONTROLE ~
    Q356=+1   ;MEETRICHTING ~
    Q357=+0   ;RADIALE OFFSET
  ;  
  ; #7.2 - TAPPEN M2 
  ;  
  TOOL CALL 54 Z S2000 ; TAP M2 - ATORN 11290922 
  M3 M8
  ;  
  
  ;  
  ; GATENPATROON BEVESTING T-LAT 
  CYCL DEF 209 SCHRDR.BOR. SPAANBR. ~
    Q200=1    ;VEILIGHEIDSAFSTAND ~
    Q201=-6   ;DRAADDIEPTE ~
 Q239=+0.4 ;SPOED ~
    Q203=-3.5 ;COORD. OPPERVLAK ~
    Q204=10   ;2e VEILIGHEIDSAFST. ~
    Q257=6    ;BOORDPTE SPAANBREUK ~
    Q256=0    ;TERUGTR.HGT SPAANBR. ~
    Q336=+0   ;HOEK SPIL
  CYCL DEF 221 MODEL OP LIJN ~
    Q225=-15  ;STARTPUNT 1e AS ~
    Q226=-7.8 ;STARTPUNT 2e AS ~
    Q237=+10  ;AFSTAND 1e AS ~
    Q238=+0   ;AFSTAND 2e AS ~
    Q242=+4    ;AANTAL KOLOMMEN ~
    Q243=1    ;AANTAL REGELS ~
    Q224=+0   ;ROTATIEPOSITIE ~
    Q200=1    ;VEILIGHEIDSAFSTAND ~
    Q203=-3.5 ;COORD. OPPERVLAK ~
    Q204=10   ;2e VEILIGHEIDSAFST. ~
    Q301=1    ;VERPL.VEILIGH.HOOGTE
  ;  
  ; GATENPATROON EINDESLAG 
  CYCL DEF 209 SCHRDR.BOR. SPAANBR. ~
    Q200=1    ;VEILIGHEIDSAFSTAND ~
    Q201=-6   ;DRAADDIEPTE ~
    Q239=+0.4 ;SPOED ~
    Q203=-3.5 ;COORD. OPPERVLAK ~
    Q204=10   ;2e VEILIGHEIDSAFST. ~
    Q257=6    ;BOORDPTE SPAANBREUK ~
    Q256=0    ;TERUGTR.HGT SPAANBR. ~
    Q336=+0   ;HOEK SPIL
  L X-7 Y-3.5 Z+10 F MAX M99
  
L X-4.5 Y-3.5 Z+10 F MAX M99
  
L X-2 Y-3.5 Z+10 F MAX M99
  
L X+2 Y-3.5 Z+10 F MAX M99
  
L X+4.5 Y-3.5 Z+10 F MAX M99
  
L X+7 Y-3.5 Z+10 F MAX M99
  

  ;  
  TCH PROBE 586 WT-BREUKCONTROLE ~
    Q356=+1   ;MEETRICHTING ~
    Q357=+0   ;RADIALE OFFSET
  
  ; ------------------------------ 
  ; #8 - LOSFREZEN STUK 
  ; ------------------------------ 
  ;  
  ; RONDOM ROND DIEPER FREZEN TOT NET VOOR LOSKOMEN 
  ;  
  TOOL CALL 14 Z S28500 ; FREES D4 FINISSEER - WNT 53 635 04300 
  M3 M8
  CYCL DEF 14.0 CONTOUR
  CYCL DEF 14.1 CONTOURLABEL 1
  CYCL DEF 25.0 CONTOURREEKS ~
    Q1=-1.5   ;FREESDIEPTE ~
    Q3=+0     ;OVERMAAT ZIJKANT ~
    Q5=-6     ;COORD. OPPERVLAK ~
    Q7=+1     ;VEILIGE HOOGTE ~
    Q10=+1    ;DIEPTEVERPLAATSING ~
    Q11=300   ;AANZET DIEPTEVERPLAATSING ~
    Q12=1500  ;AANZET FREZEN ~
    Q15=+1    ;FREESWIJZE
  L X+0 Y+0 Z+10 F MAX M99
  CYCL DEF 25.0 CONTOURREEKS ~
    Q1=-7.5   ;FREESDIEPTE ~
    Q3=-0.01  ;OVERMAAT ZIJKANT ~
    Q5=+0     ;COORD. OPPERVLAK ~
    Q7=+1     ;VEILIGE HOOGTE ~
    Q10=+1.5  ;DIEPTEVERPLAATSING ~
    Q11=300   ;AANZET DIEPTEVERPLAATSING ~
    Q12=1500  ;AANZET FREZEN ~
    Q15=+1    ;FREESWIJZE
  L X+0 Y+0 Z+10 F MAX M99
  ;  
  ; STOP M30 
  ;  
  CALL LBL 99 ; STOP HIER BIJ PGM CALL VANUIT ARRAY_ONDER 
  ;  
  ; ------------------------------ 
  ; LABELS - CONTOUREN 
  ; ------------------------------ 
  ;  
    LBL 1 ; CONTOUR RONDOM ROND 
  L X+0 Y+23.8 RL
  L X+20 Y+23.8
  CHF 0.3
  L X+20 Y-23.8
  CHF 0.3
  L X-20 Y-23.8
  CHF 0.3
  L X-20 Y+23.8
  CHF 0.3
  L X+0 Y+23.8
  LBL 0
  ;  
  LBL 2 ; CONTOUR RONDOM MET FIXATIEPUNTEN (BOVEN) 
  L X-20 Y+20 RL
  L X-20 Y+23.8
  CHF 0.3
  L X+20 Y+23.8
  CHF 0.3
  L X+20 Y+20
  LBL 0
  ;  
  LBL 3 ; CONTOUR RONDOM MET FIXATIEPUNTEN (ONDER) 
  L X-20 Y-20 RR
  L X-20 Y-23.8
  CHF 0.3
  L X+20 Y-23.8
  CHF 0.3
  L X+20 Y-20
  LBL 0
  ;  
  LBL 4 ; CONTOUR RONDOM MET FIXATIEPUNTEN (LINKS) 
  L X-20 Y-15 RL
  L X-20 Y+15
  LBL 0
  ;  
  LBL 5 ; CONTOUR RONDOM MET FIXATIEPUNTEN (RECHTS) 
  L X+20 Y-15 RR
  L X+20 Y+15
  LBL 0
  ;  
  ; 535  STOP M30 
  STOP M30
  ;  
  LBL 99 ; LABEL OM DOOR TE GAAN NAAR PROGRAMMA EINDE BIJ PGM CALL
  ;
  END PGM ONDER_TEST MM