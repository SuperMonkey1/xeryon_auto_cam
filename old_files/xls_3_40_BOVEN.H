  BEGIN PGM BOVEN MM
  ;  
  ; WERKSTUK: TOP PLATE 1103_040_002_G2 - BEWERKING BOVEN 
  ;  
  BLK FORM 0.1 Z X-20 Y-23.8 Z-7.5
  BLK FORM 0.2 X+20 Y+23.8 Z+0.2
  ;  
  ; Z-REF OPMETEN OP BOVENKANT VAN OPLEGVLAK OPSPANTOOL (Z-4) 
  ; OPSPANNEN MET 2x EXCENTRISCHE SPANKLEM OP 10 cNm 
  ;  
  ; ONDERZIJDE WERKSTUK ONTBRAMEN (ZEKER HOEKJE TRACTIEZIJDE) 
  ; BETER OPLOSSING IS OM GROEFJES TE VOORZIEN IN OPSPANTOOL 
  ;  
  ; AANPASSINGEN VOOR VERSNELDE PRODUCTIE NOG DOORVOEREN (OK VOOR  ~
    XLS-3-40) 
  ;  
  ; TOLERANTIE UITLIJNGATEN CONTROLEREN MET STELPEN D2 h6 
  ;  
  M36 ; SPAANAFVOER AAN 
  M38 ; AFZUIGING AAN 
  ;  
  ; ------------------------------ 
  ; #1 - FREZEN VERZINKINGEN 
  ; ------------------------------ 
  ;  
  ; #1.1 - FREZEN COUNTERBORE M2 
  ;  
  TOOL CALL 4 Z S28500 ; FREES D4 - WNT 53 635 04300 
  M3 M8
  CYCL DEF 5.0 RONDKAMER
  CYCL DEF 5.1 AFST.  1
  CYCL DEF 5.2 DIEPTE -2.2
  CYCL DEF 5.3 VERPL.  0.5 F50
  CYCL DEF 5.4 RADIUS 2.2
  CYCL DEF 5.5 F100 DR-
  L X-15 Y-17.1 Z+1 F MAX M99
  L X-5 Y-17.1 Z+1 F MAX M99
  L X+5 Y-17.1 Z+1 F MAX M99
  L X+15 Y-17.1 Z+1 F MAX M99
  L X-15 Y+17.1 Z+1 F MAX M99
  L X-5 Y+17.1 Z+1 F MAX M99
  L X+5 Y+17.1 Z+1 F MAX M99
  L X+15 Y+17.1 Z+1 F MAX M99
  ;  
  ; #1.2 - FREZEN VERZINKING EINDELOOP 
  ;  
  TOOL CALL 4 Z S28500 ; FREES D4 - WNT 53 635 04300 
  M3 M8
  CYCL DEF 210 SLEUF PENDELEND ~
    Q200=1    ;VEILIGHEIDSAFSTAND ~
    Q201=-0.7 ;DIEPTE ~
    Q207=500  ;AANZET FREZEN ~
    Q202=0.35 ;DIEPTEVERPLAATSING ~
    Q215=0    ;BEWERKINGSOMVANG ~
    Q203=+0   ;COORD. OPPERVLAK ~
    Q204=10   ;2e VEILIGHEIDSAFST. ~
    Q216=+0   ;MIDDEN 1e AS ~
    Q217=-3.5 ;MIDDEN 2e AS ~
    Q218=18.4 ;LENGTE 1e ZIJKANT ~
    Q219=4.4  ;LENGTE 2e ZIJKANT ~
    Q224=+0   ;ROTATIEPOSITIE ~
    Q338=0    ;AANZET NABEWERKING
  L X+Q216 Y+Q217 Z+10 F MAX M99
  ;  
  ; ------------------------------ 
  ; #2 - BOREN GATEN 
  ; ------------------------------  
  ;  
  TOOL CALL 12 Z S35000 ; BOOR D1,3 - WNT 11 770 01300 
  M3 M8
  ;  
  ; PATROON 20 X 30.4 MM (ENKEL NIET-TRACTIEZIJDE) 
  CYCL DEF 200 BOREN ~
    Q200=1    ;VEILIGHEIDSAFSTAND ~
    Q201=-6   ;DIEPTE ~
    Q206=260  ;AANZET DIEPTEVERPL. ~
    Q202=1    ;DIEPTEVERPLAATSING ~
    Q210=0    ;STILSTANDSTIJD BOVEN ~
    Q203=+0   ;COORD. OPPERVLAK ~
    Q204=10   ;2e VEILIGHEIDSAFST. ~
    Q211=0    ;STILSTANDSTIJD ONDER
  CYCL DEF 221 MODEL OP LIJN ~
    Q225=-10  ;STARTPUNT 1e AS ~
    Q226=+15.2 ;STARTPUNT 2e AS ~
    Q237=+20  ;AFSTAND 1e AS ~
    Q238=+0   ;AFSTAND 2e AS ~
    Q242=2    ;AANTAL KOLOMMEN ~
    Q243=1    ;AANTAL REGELS ~
    Q224=+0   ;ROTATIEPOSITIE ~
    Q200=1    ;VEILIGHEIDSAFSTAND ~
    Q203=+0   ;COORD. OPPERVLAK ~
    Q204=10   ;2e VEILIGHEIDSAFST. ~
    Q301=1    ;VERPL.VEILIGH.HOOGTE
  ; PILOOTBOREN UITLIJNGATEN D2 H7 
  CYCL DEF 200 BOREN ~
    Q200=1    ;VEILIGHEIDSAFSTAND ~
    Q201=-3.5 ;DIEPTE ~
    Q206=260  ;AANZET DIEPTEVERPL. ~
    Q202=1    ;DIEPTEVERPLAATSING ~
    Q210=0    ;STILSTANDSTIJD BOVEN ~
    Q203=+0   ;COORD. OPPERVLAK ~
    Q204=10   ;2e VEILIGHEIDSAFST. ~
    Q211=0    ;STILSTANDSTIJD ONDER
  CYCL DEF 221 MODEL OP LIJN ~
    Q225=+0   ;STARTPUNT 1e AS ~
    Q226=-17.5 ;STARTPUNT 2e AS ~
    Q237=+0   ;AFSTAND 1e AS ~
    Q238=+35  ;AFSTAND 2e AS ~
    Q242=1    ;AANTAL KOLOMMEN ~
    Q243=2    ;AANTAL REGELS ~
    Q224=+0   ;ROTATIEPOSITIE ~
    Q200=1    ;VEILIGHEIDSAFSTAND ~
    Q203=+0   ;COORD. OPPERVLAK ~
    Q204=10   ;2e VEILIGHEIDSAFST. ~
    Q301=1    ;VERPL.VEILIGH.HOOGTE
  ;   
  ; ------------------------------ 
  ; #3 - FREZEN UITLIJNGATEN D2 H7 
  ; TOLERANTIE CONTROLEREN MET STELPEN D2 h6 
  ; ------------------------------  
  ;  
  TOOL CALL 1 Z S35000 DR-0.008 ; FREES D1 - WNT 52 802 10300 
  M3 M8
  CYCL DEF 214 RONDK. NABEWERKEN ~
    Q200=1    ;VEILIGHEIDSAFSTAND ~
    Q201=-3.5 ;DIEPTE ~
    Q206=150  ;AANZET DIEPTEVERPL. ~
    Q202=0.6  ;DIEPTEVERPLAATSING ~
    Q207=270  ;AANZET FREZEN ~
    Q203=+0   ;COORD. OPPERVLAK ~
    Q204=10   ;2e VEILIGHEIDSAFST. ~
    Q216=+0   ;MIDDEN 1e AS ~
    Q217=-17.5 ;MIDDEN 2e AS ~
    Q222=1.3  ;DIAMETER RUWDEEL ~
    Q223=2.005 ;DIAMETER EINDPRODUCT
  L X+Q216 Y+Q217 Z+10 F MAX M99
  Q216 = 0   ; MIDDEN 1e AS 
  Q217 = 17.5   ; MIDDEN 2e AS 
  L X+Q216 Y+Q217 Z+10 F MAX M99
  ;  
  ; ------------------------------ 
  ; #4 - AFSCHUINEN GATEN 
  ; ------------------------------ 
  ;  
  ; #4.1 - AFSCHUINEN DIA. 2.4 MM 
  ;  
  TOOL CALL 43 Z S5000 ; CENTERBOOR D3 - WNT 10 702 003 
  M3
  CYCL DEF 200 BOREN ~
    Q200=1    ;VEILIGHEIDSAFSTAND ~
    Q201=-0.3 ;DIEPTE ~
    Q206=300  ;AANZET DIEPTEVERPL. ~
    Q202=0.3  ;DIEPTEVERPLAATSING ~
    Q210=0    ;STILSTANDSTIJD BOVEN ~
    Q203=-3.2 ;COORD. OPPERVLAK ~
    Q204=10   ;2e VEILIGHEIDSAFST. ~
    Q211=0    ;STILSTANDSTIJD ONDER
  CYCL DEF 221 MODEL OP LIJN ~
    Q225=-15  ;STARTPUNT 1e AS ~
    Q226=-17.1 ;STARTPUNT 2e AS ~
    Q237=+10  ;AFSTAND 1e AS ~
    Q238=+34.2 ;AFSTAND 2e AS ~
    Q242=4    ;AANTAL KOLOMMEN ~
    Q243=2    ;AANTAL REGELS ~
    Q224=+0   ;ROTATIEPOSITIE ~
    Q200=1    ;VEILIGHEIDSAFSTAND ~
    Q203=-3.2 ;COORD. OPPERVLAK ~
    Q204=10   ;2e VEILIGHEIDSAFST. ~
    Q301=1    ;VERPL.VEILIGH.HOOGTE
  ;  
  ; #4.2 - AFSCHUINEN GATEN M2.5 
  ;  
  TOOL CALL 43 Z S5000 ; CENTERBOOR D3 - WNT 10 702 003 
  M3
  CYCL DEF 200 BOREN ~
    Q200=1    ;VEILIGHEIDSAFSTAND ~
    Q201=-0.35 ;DIEPTE ~
    Q206=300  ;AANZET DIEPTEVERPL. ~
    Q202=0.35 ;DIEPTEVERPLAATSING ~
    Q210=0    ;STILSTANDSTIJD BOVEN ~
    Q203=-1   ;COORD. OPPERVLAK ~
    Q204=10   ;2e VEILIGHEIDSAFST. ~
    Q211=0    ;STILSTANDSTIJD ONDER
  CYCL DEF 221 MODEL OP LIJN ~
    Q225=-15  ;STARTPUNT 1e AS ~
    Q226=-22  ;STARTPUNT 2e AS ~
    Q237=+10  ;AFSTAND 1e AS ~
    Q238=+44  ;AFSTAND 2e AS ~
    Q242=4    ;AANTAL KOLOMMEN ~
    Q243=2    ;AANTAL REGELS ~
    Q224=+0   ;ROTATIEPOSITIE ~
    Q200=1    ;VEILIGHEIDSAFSTAND ~
    Q203=-1   ;COORD. OPPERVLAK ~
    Q204=10   ;2e VEILIGHEIDSAFST. ~
    Q301=1    ;VERPL.VEILIGH.HOOGTE
  ;  
  ; #4.3 - AFSCHUINEN GATEN M2 
  ;  
  TOOL CALL 43 Z S5000 ; CENTERBOOR D3 - WNT 10 702 003 
  M3
  ;   
  ; GATENPATROON 42 X 39 MM (NIET VOOR XLS-3-40) 
  CYCL DEF 200 BOREN ~
    Q200=1    ;VEILIGHEIDSAFSTAND ~
    Q201=-1   ;DIEPTE ~
    Q206=300  ;AANZET DIEPTEVERPL. ~
    Q202=1    ;DIEPTEVERPLAATSING ~
    Q210=0    ;STILSTANDSTIJD BOVEN ~
    Q203=+0   ;COORD. OPPERVLAK ~
    Q204=10   ;2e VEILIGHEIDSAFST. ~
    Q211=0    ;STILSTANDSTIJD ONDER
  ; L X-21 Y-19.5 Z+10 F MAX M99 
  ; L X+21 Y-19.5 Z+10 F MAX M99 
  ; L X+21 Y+19.5 Z+10 F MAX M99 
  ; L X-21 Y+19.5 Z+10 F MAX M99 
  ;  
  ; GATENPATROON BEVESTING T-LAT 
  CYCL DEF 200 BOREN ~
    Q200=1    ;VEILIGHEIDSAFSTAND ~
    Q201=-1   ;DIEPTE ~
    Q206=300  ;AANZET DIEPTEVERPL. ~
    Q202=1    ;DIEPTEVERPLAATSING ~
    Q210=0    ;STILSTANDSTIJD BOVEN ~
    Q203=+0   ;COORD. OPPERVLAK ~
    Q204=10   ;2e VEILIGHEIDSAFST. ~
    Q211=0    ;STILSTANDSTIJD ONDER
  CYCL DEF 221 MODEL OP LIJN ~
    Q225=-15  ;STARTPUNT 1e AS ~
    Q226=-7.8 ;STARTPUNT 2e AS ~
    Q237=+10  ;AFSTAND 1e AS ~
    Q238=+0   ;AFSTAND 2e AS ~
    Q242=4    ;AANTAL KOLOMMEN ~
    Q243=1    ;AANTAL REGELS ~
    Q224=+0   ;ROTATIEPOSITIE ~
    Q200=1    ;VEILIGHEIDSAFSTAND ~
    Q203=+0   ;COORD. OPPERVLAK ~
    Q204=10   ;2e VEILIGHEIDSAFST. ~
    Q301=1    ;VERPL.VEILIGH.HOOGTE
  ;  
  ; GATENPATROON EINDESLAG 
  CYCL DEF 200 BOREN ~
    Q200=1    ;VEILIGHEIDSAFSTAND ~
    Q201=-0.8 ;DIEPTE ~
    Q206=300  ;AANZET DIEPTEVERPL. ~
    Q202=0.8  ;DIEPTEVERPLAATSING ~
    Q210=0    ;STILSTANDSTIJD BOVEN ~
    Q203=-1   ;COORD. OPPERVLAK ~
    Q204=10   ;2e VEILIGHEIDSAFST. ~
    Q211=0    ;STILSTANDSTIJD ONDER
  L X-7 Y-3.5 Z+10 F MAX M99
  L X-4.5 Y-3.5 Z+10 F MAX M99
  L X-2 Y-3.5 Z+10 F MAX M99
  L X+2 Y-3.5 Z+10 F MAX M99
  L X+4.5 Y-3.5 Z+10 F MAX M99
  L X+7 Y-3.5 Z+10 F MAX M99
  ;  
  ; #4.4 - AFSCHUINEN GATEN M1.6 
  ;  
  TOOL CALL 43 Z S5000 ; CENTERBOOR D3 - WNT 10 702 003 
  M3
  ;  
  ; PATROON 20 X 30.4 MM 
  CYCL DEF 200 BOREN ~
    Q200=1    ;VEILIGHEIDSAFSTAND ~
    Q201=-0.8 ;DIEPTE ~
    Q206=300  ;AANZET DIEPTEVERPL. ~
    Q202=0.8  ;DIEPTEVERPLAATSING ~
    Q210=0    ;STILSTANDSTIJD BOVEN ~
    Q203=+0   ;COORD. OPPERVLAK ~
    Q204=10   ;2e VEILIGHEIDSAFST. ~
    Q211=0    ;STILSTANDSTIJD ONDER
  CYCL DEF 221 MODEL OP LIJN ~
    Q225=-10  ;STARTPUNT 1e AS ~
    Q226=-15.2 ;STARTPUNT 2e AS ~
    Q237=+20  ;AFSTAND 1e AS ~
    Q238=+30.4 ;AFSTAND 2e AS ~
    Q242=2    ;AANTAL KOLOMMEN ~
    Q243=2    ;AANTAL REGELS ~
    Q224=+0   ;ROTATIEPOSITIE ~
    Q200=1    ;VEILIGHEIDSAFSTAND ~
    Q203=+0   ;COORD. OPPERVLAK ~
    Q204=10   ;2e VEILIGHEIDSAFST. ~
    Q301=1    ;VERPL.VEILIGH.HOOGTE
  ;  
  ; PATROON 20 X 13 MM 
  CYCL DEF 200 BOREN ~
    Q200=1    ;VEILIGHEIDSAFSTAND ~
    Q201=-0.8 ;DIEPTE ~
    Q206=300  ;AANZET DIEPTEVERPL. ~
    Q202=0.8  ;DIEPTEVERPLAATSING ~
    Q210=0    ;STILSTANDSTIJD BOVEN ~
    Q203=+0   ;COORD. OPPERVLAK ~
    Q204=10   ;2e VEILIGHEIDSAFST. ~
    Q211=0    ;STILSTANDSTIJD ONDER
  CYCL DEF 221 MODEL OP LIJN ~
    Q225=-10  ;STARTPUNT 1e AS ~
    Q226=-6.5 ;STARTPUNT 2e AS ~
    Q237=+20  ;AFSTAND 1e AS ~
    Q238=+13  ;AFSTAND 2e AS ~
    Q242=2    ;AANTAL KOLOMMEN ~
    Q243=2    ;AANTAL REGELS ~
    Q224=+0   ;ROTATIEPOSITIE ~
    Q200=1    ;VEILIGHEIDSAFSTAND ~
    Q203=+0   ;COORD. OPPERVLAK ~
    Q204=10   ;2e VEILIGHEIDSAFST. ~
    Q301=1    ;VERPL.VEILIGH.HOOGTE
  ;  
  ; PATROON 30.4 X 20 MM 
  CYCL DEF 200 BOREN ~
    Q200=1    ;VEILIGHEIDSAFSTAND ~
    Q201=-0.8 ;DIEPTE ~
    Q206=300  ;AANZET DIEPTEVERPL. ~
    Q202=0.8  ;DIEPTEVERPLAATSING ~
    Q210=0    ;STILSTANDSTIJD BOVEN ~
    Q203=+0   ;COORD. OPPERVLAK ~
    Q204=10   ;2e VEILIGHEIDSAFST. ~
    Q211=0    ;STILSTANDSTIJD ONDER
  CYCL DEF 221 MODEL OP LIJN ~
    Q225=-15.2 ;STARTPUNT 1e AS ~
    Q226=-10  ;STARTPUNT 2e AS ~
    Q237=+30.4 ;AFSTAND 1e AS ~
    Q238=+20  ;AFSTAND 2e AS ~
    Q242=2    ;AANTAL KOLOMMEN ~
    Q243=2    ;AANTAL REGELS ~
    Q224=+0   ;ROTATIEPOSITIE ~
    Q200=1    ;VEILIGHEIDSAFSTAND ~
    Q203=+0   ;COORD. OPPERVLAK ~
    Q204=10   ;2e VEILIGHEIDSAFST. ~
    Q301=1    ;VERPL.VEILIGH.HOOGTE
  ;  
  ; #4.5 - AFSCHUINEN UITLIJNGATEN D2 H7 
  ;  
  TOOL CALL 43 Z S5000 ; CENTERBOOR D3 - WNT 10 702 003 
  M3
  ;  
  CYCL DEF 200 BOREN ~
    Q200=1    ;VEILIGHEIDSAFSTAND ~
    Q201=-0.05 ;DIEPTE ~
    Q206=300  ;AANZET DIEPTEVERPL. ~
    Q202=0.05 ;DIEPTEVERPLAATSING ~
    Q210=0    ;STILSTANDSTIJD BOVEN ~
    Q203=-1   ;COORD. OPPERVLAK ~
    Q204=10   ;2e VEILIGHEIDSAFST. ~
    Q211=0    ;STILSTANDSTIJD ONDER
  CYCL DEF 221 MODEL OP LIJN ~
    Q225=+0   ;STARTPUNT 1e AS ~
    Q226=-17.5 ;STARTPUNT 2e AS ~
    Q237=+0   ;AFSTAND 1e AS ~
    Q238=+35  ;AFSTAND 2e AS ~
    Q242=1    ;AANTAL KOLOMMEN ~
    Q243=2    ;AANTAL REGELS ~
    Q224=+0   ;ROTATIEPOSITIE ~
    Q200=1    ;VEILIGHEIDSAFSTAND ~
    Q203=-1   ;COORD. OPPERVLAK ~
    Q204=10   ;2e VEILIGHEIDSAFST. ~
    Q301=1    ;VERPL.VEILIGH.HOOGTE
  ;   
  ; #4.6 - AFSCHUINEN COUNTERBORE BUITEN 
  ;  
  TOOL CALL 46 Z S5000 ; CENTERBOOR D6 - WNT 10 702 006 
  M3
  CYCL DEF 200 BOREN ~
    Q200=1    ;VEILIGHEIDSAFSTAND ~
    Q201=-0.3 ;DIEPTE ~
    Q206=300  ;AANZET DIEPTEVERPL. ~
    Q202=0.3  ;DIEPTEVERPLAATSING ~
    Q210=0    ;STILSTANDSTIJD BOVEN ~
    Q203=-2   ;COORD. OPPERVLAK ~
    Q204=10   ;2e VEILIGHEIDSAFST. ~
    Q211=0    ;STILSTANDSTIJD ONDER
  CYCL DEF 221 MODEL OP LIJN ~
    Q225=-15  ;STARTPUNT 1e AS ~
    Q226=-17.1 ;STARTPUNT 2e AS ~
    Q237=+10  ;AFSTAND 1e AS ~
    Q238=+34.2 ;AFSTAND 2e AS ~
    Q242=4    ;AANTAL KOLOMMEN ~
    Q243=2    ;AANTAL REGELS ~
    Q224=+0   ;ROTATIEPOSITIE ~
    Q200=1    ;VEILIGHEIDSAFSTAND ~
    Q203=-2   ;COORD. OPPERVLAK ~
    Q204=10   ;2e VEILIGHEIDSAFST. ~
    Q301=1    ;VERPL.VEILIGH.HOOGTE
  ;  
  ; ------------------------------ 
  ; #5 - TAPPEN 
  ; ------------------------------ 
  ;  
  TOOL CALL 61 Z S2500 ; TAP M1.6 - ATORN 11290919 
  M3 M8
  ;  
  ; PATROON 20 X 30.4 MM (ENKEL NIET-TRACTIEZIJDE) 
  CYCL DEF 209 SCHRDR.BOR. SPAANBR. ~
    Q200=1    ;VEILIGHEIDSAFSTAND ~
    Q201=-5   ;DRAADDIEPTE ~
    Q239=+0.33 ;SPOED ~
    Q203=+0   ;COORD. OPPERVLAK ~
    Q204=10   ;2e VEILIGHEIDSAFST. ~
    Q257=1.5  ;BOORDPTE SPAANBREUK ~
    Q256=0    ;TERUGTR.HGT SPAANBR. ~
    Q336=+0   ;HOEK SPIL
  CYCL DEF 221 MODEL OP LIJN ~
    Q225=-10  ;STARTPUNT 1e AS ~
    Q226=+15.2 ;STARTPUNT 2e AS ~
    Q237=+20  ;AFSTAND 1e AS ~
    Q238=+0   ;AFSTAND 2e AS ~
    Q242=2    ;AANTAL KOLOMMEN ~
    Q243=1    ;AANTAL REGELS ~
    Q224=+0   ;ROTATIEPOSITIE ~
    Q200=1    ;VEILIGHEIDSAFSTAND ~
    Q203=+0   ;COORD. OPPERVLAK ~
    Q204=10   ;2e VEILIGHEIDSAFST. ~
    Q301=1    ;VERPL.VEILIGH.HOOGTE
  ;  
  ; ------------------------------ 
  ; #6 - AFVLAKKEN BOVENZIJDE TOT Z+0 
  ; ------------------------------ 
  ;  
  TOOL CALL 10 Z S11500 ; FREES D10 - WNT 53 625 10100 
  M3 M8
  CYCL DEF 230 AFFREZEN ~
    Q225=-21  ;STARTPUNT 1e AS ~
    Q226=-30  ;STARTPUNT 2e AS ~
    Q227=+0.4 ;STARTPUNT 3e AS ~
    Q218=42   ;LENGTE 1e ZIJKANT ~
    Q219=60   ;LENGTE 2e ZIJKANT ~
    Q240=8    ;AANTAL SNEDEN ~
    Q206=300  ;AANZET DIEPTEVERPL. ~
    Q207=1500 ;AANZET FREZEN ~
    Q209=1500 ;AANZET DWARS ~
    Q200=1    ;VEILIGHEIDSAFSTAND
  L X+0 Y+0 Z+10 F MAX M99
  Q227 = 0.15   ; STARTPUNT 3e AS 
  L X+0 Y+0 Z+10 F MAX M99
  FN 0: Q240 = +12 ; AANTAL SNEDEN 
  Q227 = 0.04   ; STARTPUNT 3e AS 
  L X+0 Y+0 Z+10 F MAX M99
  ;  
  ; ------------------------------ 
  ; #7 - AFSCHUINEN 
  ; ------------------------------ 
  ;  
  TOOL CALL 40 Z S15000 DL-0.5 DR-1.5 ; ONTBRAAM_D4 - WNT 53 660 04000 
  M3 M8
  ;  
  ; BUITENRAND 
  L X+0 Y+30 Z+10 R0 F MAX
  L Z+1 F MAX
  APPR LN X+0 Y+23.8 LEN+3 RL F1000
  L Z-0.3 F1000
  L X+20 F1000
  CHF 0.3 F100
  L Y-23.8 F1000
  CHF 0.3 F100
  L X-20 F1000
  CHF 0.3 F100
  L Y+23.8 F1000
  CHF 0.3 F100
  L X+0 F1000
  DEP LN LEN+3 F1000
  L Z+1 R0 F1000
  ;  
  TOOL CALL 40 Z S15000 DL-0.5 DR-1.5 ; ONTBRAAM_D4 - WNT 53 660 04000 
  M3
  ;  
  ; SLEUF EINDESLAG  
  L X+0 Y-3.5 Z+1 R0 F MAX
  APPR LN X+0 Y-5.7 LEN+1 RL F500
  L Z-0.05 F1000
  L X+7 Y-5.7 F500
  CC X+7 Y-3.5
  C X+7 Y-1.3 DR+ F500
  L X-7 Y-1.3 F500
  CC X-7 Y-3.5
  C X-7 Y-5.7 DR+ F500
  L X+0 Y-5.7 F500
  DEP LN LEN+1 F500
  L Z+1 R0 F500
  L Z+10 R0 F MAX
  ;  
  L X-120 Z+150 R0 F MAX
  L X-294 Y+310 Z+260 R0 F MAX
  ;  
  STOP M30
  ;  
  END PGM BOVEN MM
